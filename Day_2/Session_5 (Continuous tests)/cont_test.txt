model {
  for (i in 1:481) {
	    #S[i] diagnostic test value for ith individual
	    S[i] ~ dnorm(mu[i],tau[i])
  
	    #Value of mu & tau depending on the group (diseased or disease-free)
	    mu[i] <- lambda[T[i]]           
	    tau[i] <- gamma[T[i]]
	    #dcat <- categorical #D(-) if T[i]=1, D(+) if T[i]=2
	    T[i] ~ dcat(P[]) 
  }
	P[1:2] ~ ddirch(alpha[])

	# lambda[1]-gamma[1] mean-precision of non-disease group   
	lambda[1] ~ dnorm(0,0.001)
	lambda[2] ~ dnorm(0,0.001)T(lambda[1],) 
	gamma[1] ~ dgamma(0.001,0.001)
	gamma[2] ~ dgamma(0.001,0.001)  

	# variance = 1/precision(tau)
	sigma[1] <- 1/gamma[1] 	
	sigma[2] <- 1/gamma[2]	

	# AUC
	AUC <- phi(-(lambda[1]-lambda[2])/sqrt(sigma[2]+sigma[1]))
	# ROC curve
	for(i in 1:111) {
	  c1[i] <-  ((-8.1+0.1*i)-lambda[2])/sqrt(sigma[2]) # grid is from -3 to 8
	  se[i] <- 1-phi(c1[i])
    c2[i] <-  ((-8.1+0.1*i)-lambda[1])/sqrt(sigma[1])
	  sp[i] <- phi(c2[i])
	  
	  Y[i] <- se[i] + sp[i] - 1
	}
	#data# alpha, S
	#inits# lambda, gamma
	#monitor# AUC, se, sp, P, lambda, gamma, sigma
}
